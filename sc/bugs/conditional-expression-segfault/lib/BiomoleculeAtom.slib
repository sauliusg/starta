/*--*- Pascal -*-------------------------------------------------------------*\
**$Author: saulius $
**$Date: 2017-05-08 18:44:10 +0300 (Mon, 08 May 2017) $ 
**$Revision: 3485 $
**$URL: svn+ssh://saulius-grazulis.lt/home/saulius/svn-repositories/paskaitos/VU/software/trunk/bioinformatika-III-assignments/lib/BiomoleculeAtom.slib $
\*---------------------------------------------------------------------------*/
/*
  A Starta module to define description of a biomolecule atom, used
  for representing data from PDB, CIF and PDBx files.
*/

module BiomoleculeAtom;

use std;

type Atom = class
    begin
       recordType : string; (* ATOM or HETATM *)
       string chemType;
       string atomName;
       long atomNumber;
       string residueName;
       long residueNumber;
       string altLoc; (* alternative location indicator *)
       string insCode; (* insertion code *)
       string chain;
       double B;
       double occupancy;
       double x, y, z; (* Cartesian coordinates *)

       (* the empty default constructor leaves all fields initialised
        with zeros and null pointers: *)
       constructor() {};
       
       constructor fromNumbers(recordType: string = "ATOM";
                               chemType: string;
                               atomName: string;
                               atomNumber: long;
                               residueName: string;
                               residueNumber: long;
                               chainId: string;
                               x: double;
                               y: double;
                               z: double;
                               Bfactor: double = 0.0;
                               occupancy: double = 1.0;
                               altLoc: string = "";
                               insCode: string = ""
                               )
       begin
          self.recordType = recordType;
          self.chemType = chemType;
          self.atomName = atomName;
          self.atomNumber = atomNumber;
          self.residueName = residueName;
          self.residueNumber = residueNumber;
          self.chain = chainId;
          self.x = x;
          self.y = y;
          self.z = z;
          self.B = Bfactor;
          self.occupancy = occupancy;
          self.altLoc = altLoc;
          self.insCode = insCode;
       end;

       constructor fromStrings(recordType: string = "ATOM";
                               chemType: string;
                               atomName: string;
                               atomNumber: string;
                               residueName: string;
                               residueNumber: string;
                               chainId: string;
                               x: string;
                               y: string;
                               z: string;
                               Bfactor: string = "0.0";
                               occupancy: string = "1.0";
                               altLoc: string = "";
                               insCode: string = ""
                               )
       begin
          self.recordType = recordType;
          self.chemType = chemType;
          self.atomName = atomName;
          self.atomNumber = strtol( atomNumber );
          self.residueName = residueName;
          self.residueNumber = strtol( residueNumber );
          self.chain = chainId;
          self.x = strtod( x );
          self.y = strtod( y );
          self.z = strtod( z );
          self.B = strtod( Bfactor );
          self.occupancy = strtod( occupancy );
          self.altLoc = altLoc;
          self.insCode = insCode;
       end;

       method asString( printAtomNumber: int = 1 ): string
       begin
          return
          "%-6s " %% self.recordType _
          "%-2s " %% self.chemType _
          (printAtomNumber == 1 ? ("%7d" %% self.atomNumber) : "") _
          (self.altLoc == "" ? " " : self.altLoc) _
          "%5s "  %% self.atomName _
          "%5s "  %% self.residueName _
          "%5d"   %% self.residueNumber _
          (self.insCode == "" ? " " : self.insCode) _
          "%2s "  %% self.chain _
          "%8g "  %% self.x _
          "%8g "  %% self.y _
          "%8g "  %% self.z _
          "%6g "  %% self.B _
          "%6g"   %% self.occupancy;
       end;
       
    end; (* class Atom *)

end module BiomoleculeAtom;
