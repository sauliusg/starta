//--*- C -*------------------------------------------------------------------*\
//$Author: saulius $
//$Date: 2015-07-17 22:34:37 +0300 (Fri, 17 Jul 2015) $ 
//$Revision: 69 $
//$URL: svn+ssh://saulius-grazulis.lt/home/saulius/svn-repositories/sgem-xray/trunk/lib/CheckMat.slib $
//---------------------------------------------------------------------------*/
//*
// Check features of matrices. Matrices are represented as arrays of arrays.
//**

module CheckMat;

use * from std;

function is_rectangular( m : array [][] of type T ): bool
{
    if( !m ) { return true }
    if( llength(m) == 0L ) { return true }

    if( m[0] ) {
        var length = llength( m[0] );
        for( var i = 1L; i < llength(m); i ++ ) {
            if( length != llength(m[i]) ) {
                return false;
            }
        }
    } else {
        foreach var e in m {
            if( e ) { return false }
        }
    }
    return true
}

function is_square( m : array [][] of type T ): bool
{
    var long length;

    if( !m ) { return true }
    if( (length = llength(m)) == 0L ) { return true }

    foreach var line in m {
        if( length != llength(line) ) {
            return false;
        }
    }
    return true;
}

end module CheckMat;
