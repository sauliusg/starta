// --*- C -*--

// Test the new generic type implementation (a-la Ada).

use * from std;

exception UNIMPLEMENTED_METHOD;

type R = generic ref;

type Number = class {

    method print()
    {
     raise UNIMPLEMENTED_METHOD( "unimplemented method \"print\"" );
    }
};

class Integer : Number {
    int ival;

    constructor ( i : int )
    {
        self.ival = i;
    }
    
    method print()
    {
        . self.ival;
    }
}

class Float : Number {
    float fval;

    constructor ( x : float )
    {
        self.fval = x;
    }
    
    method print()
    {
        . self.fval;
    }
}

operator "+" (i : Integer; delta : int) -> Integer
{
    var Integer ret = new Integer( i.ival + delta );
    return ret;
}

operator "+" (x : Float; delta : float) -> Float
{
    var Float ret = new Float( x.fval + delta );
    return ret;
}

operator "." (i : Integer)
{
    < i.ival;
}

operator "." (f : Float)
{
    < f.fval;
}

procedure sprinter( s : string ): string { . s; return s };
procedure iprinter( i : Integer ): Integer { i->print(); return i };
procedure fprinter( x : Float ): Float { x->print(); return x };

procedure supdater( s : string ): string { return s + " with extra part" };
procedure iupdater( i : Integer ): Integer { return i + 2 };
procedure fupdater( x : Float ): Float { return x + 1.5 };

procedure generic_processor( s : R; procedure (x : R)->(R) xmanager ): R
{
    s = xmanager( s );
    return s;
}

generic_processor( "foo", sprinter );
. generic_processor( "bar", supdater );

. "\n================\n";

generic_processor( new Integer(42), iprinter );
generic_processor( new Integer(43), iupdater )->print();
. generic_processor( new Integer(43), iupdater );

. "\n================\n";

generic_processor( new Float(42.5), fprinter );
generic_processor( new Float(43), fupdater )->print();
. generic_processor( new Float(43), fupdater );
