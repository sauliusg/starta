/*--*- C -*--*/
/* PL/I version of Knuth's "man or boy" test        */
/* expected results at:                             */
/*    https://en.wikipedia.org/wiki/Man_or_boy_test */
/* This version courtesy of Robert AH Prins         */

use std;

function x1(): int begin return(1);  end;
function x2(): int begin return(-1); end;
function x3(): int begin return(-1); end;
function x4(): int begin return(1);  end;
function x5(): int begin return(0);  end;

function a( k: int; y1, y2, y3, y4, y5 : function()->(int) ): int
{
    var b = closure function()->(int)
    local {
        k: int = k;
        y1, y2, y3, y4: function()->(int) = y1, y2, y3, y4;
    }
    {
        local.k = local.k - 1;
        return a(local.k, local.self, local.y1, local.y2, local.y3, local.y4);
    };

    if k <= 0 then
        return y4() + y5();
    else
        return b();
    endif;
}

inline bytecode function set_gc_policy( int policy ): int
{
    GCPOLICY
}

set_gc_policy( 1 );
                                   
for var i = 0 to 15 do
    . 'i =', "%2d" %% i, "%12d" %% a( i, x1, x2, x3, x4, x5 ) ;
enddo;
