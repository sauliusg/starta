#
# Generic lists.
#

package Lists( type T = <TT> );

use * from std;

type list of T = ?{
    next : list of T;
    value : T;

    operator "+" ( l1, l2 : list of T ) : list of T
    {
	if( l1 && l2 ) {
	    var r = l1[];
	    var q = r;
	    var p = l1.next;
	    while( p != null ) {
		q.next = p[];
		q = q.next;
		p = p.next;
	    }
	    q.next = l2;
	    return r
	} else {
	    if( l1 ) {
		return l1
	    } else {
		return l2
	    }
	}
    }; // operator "+"

    inline bytecode operator "next"( l : list of T )
    { NEXT const((list of T).next.offset) const((list of T).value.offset) }

} // type "list of T"

procedure cons( s : type T; l : list of type T = null ) : list of type T
{
    return type list of T { value => s, next => l };
}

procedure cat( l1, l2 : list of T ) : list of T
{
    l1.next = l2;
    return l1
}

exception NullPointerException;

procedure head( l : list of type T ) : type T
{
    if( l ) {
        return l.value
    } else {
        raise NullPointerException( 123, "attempting to take head of a null list" );
    }
}

procedure tail( l : list of type T ) : list of type T
{
    if( l ) {
        return l.next
    } else {
        return null;
    }
}

// Need:

# Operator 'cat': +, _
# Operator 'cons': ::
# Operator 'head': @
# Operator 'tail': _ (unary)

end package Lists;
